# Fireworks example

vector/svg path="M -1 0 L 1 0 M 0 -1 L 0 1" ->-
vector/scale x=0.02 y=0.02 z=0.02 ->-

clone copies=5
[
  input: vector/pin ->-
  inner:clone copies=25
  [
    input: vector/pin ->-
    t: vector/translate ->-
    stroke: vector/stroke ->-
    f: vector/fade ->-
    output: vector/pin

    go: pin ->rxv.trigger ->start ->rc.trigger ->ryv.trigger
            ->fade.start
    timer ->vel_x.- ->vel_y.- ->acc_y.factor

    # Horizontal velocity is random
    vel_x: multiply ->t.x
    rxv: random min=-0.05 max=0.05 ->vel_x.factor

    # Vertical gravity acceleration
    vel_y: add ->-
    acc_y: multiply ->-
    multiply factor=-0.02 ->-
    add offset=0.25 ->t.y
    ryv: random min=-3 max=3 ->vel_y.offset

    # Random colour
    rc: random min=0 max=0.2 ->h
    colour/hsl l=0.7 ->stroke.colour

    # Fading
    fade: interpolator from=1.0 to=0.0 period=5 ->-
    twinkle: multiply ->f.alpha
    random min=0.2 max=1 ->twinkle.factor
  ] ->-

  t2: vector/translate ->-
  output: vector/pin

  clone-info fraction>-
  multiply factor=5 ->offset
  beat interval=5 ->rpx.trigger ->rpy.trigger ->inner.go

  # Random position
  rpx: random min=-0.4 max=0.4 ->t2.x
  rpy: random min=-0.2 max=0.2 ->t2.y
] ->-
vector/clip ->-
vector/websocket-display

